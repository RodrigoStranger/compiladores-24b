NETCODE -> OPCIONESNET MASFUNCIONES MAIN
NETCODE -> MAIN

OPCIONESNET -> FUNC
OPCIONESNET -> ASIG

MASFUNCIONES -> OPCIONESNET MASFUNCIONES
MASFUNCIONES -> ''

FUNC -> FUNCION ID CORCHETEABI PARAMETROS CORCHETECERR OPDATO LLAVEABI INS LLAVECERR

MAIN -> PROGRAMA PRINCIPAL TIPOENTERO CORCHETEABI CORCHETECERR LLAVEABI INS LLAVECERR

OPDATO -> TIPOVACIO
OPDATO -> TIPODATO

PARAMETROS -> '' 
PARAMETROS -> VARIABLE ID TIPODATO MASPARAMETROS

MASPARAMETROS -> COMA PARAMETROS
MASPARAMETROS -> ''

CONDICIONAL -> SI CORCHETEABI EXPRESION CORCHETECERR LLAVEABI INS LLAVECERR POSIBILIDAD

POSIBILIDAD -> SINO CORCHETEABI EXPRESION CORCHETECERR LLAVEABI INS LLAVECERR POSIBILIDAD
POSIBILIDAD -> ENTONCES LLAVEABI INS LLAVECERR
POSIBILIDAD -> ''

BUCLEWHILE -> MIENTRAS CORCHETEABI EXPRESION CORCHETECERR LLAVEABI INS LLAVECERR

BUCLEFOR -> PARA CORCHETEABI DERIVA PUNTOYCOMA EXPRESION PUNTOYCOMA ID DERIVA2 CORCHETECERR LLAVEABI INS LLAVECERR

INS -> INSTRUCCION MASINSTRUCCION
INS -> ''

DERIVA -> VARIABLE ID TIPOENTERO
DERIVA -> ID IGUAL NENTERO

DERIVA2 -> UNARIOS
DERIVA2 -> IGUAL EXPRESION

INSTRUCCION -> ASIG
INSTRUCCION -> IMP
INSTRUCCION -> BUCLEFOR
INSTRUCCION -> BUCLEWHILE
INSTRUCCION -> CONDICIONAL
INSTRUCCION -> DETENER
INSTRUCCION -> RETORNAR EXPRESION

MASINSTRUCCION -> INSTRUCCION MASINSTRUCCION
MASINSTRUCCION -> ''

IMP -> IMPRIMIR CORCHETEABI CMDS CORCHETECERR

CMDS -> ''
CMDS -> EXPRESION MASCOMANDOS

MASCOMANDOS -> CONCATENAR EXPRESION MASCOMANDOS
MASCOMANDOS -> ''

ASIG -> VARIABLE ID TIPODATO OPC
ASIG -> ID IGUAL EXPRESION

OPC -> IGUAL EXPRESION
OPC -> ''

EXPRESION -> CORCHETEABI EXPRESION CORCHETECERR MASEXPRESION
EXPRESION -> ID OPCION MASEXPRESION
EXPRESION -> DATO MASEXPRESION

MASEXPRESION -> '' 
MASEXPRESION -> OPERACION EXPRESION

OPCION -> CORCHETEABI PAR CORCHETECERR
OPCION -> ''

PAR -> TF RESTO_PARAMETROS
PAR -> ''

RESTO_PARAMETROS -> COMA TF RESTO_PARAMETROS
RESTO_PARAMETROS -> ''

TF -> DATO MASEXPRESION
TF -> ID OPCION MASEXPRESION

OPERACION -> SUMA
OPERACION -> RESTA
OPERACION -> MULTIPLICACION
OPERACION -> DIVISION
OPERACION -> RESIDUO
OPERACION -> SUMAIGUAL
OPERACION -> RESTAIGUAL
OPERACION -> MULTIPLICACIONIGUAL
OPERACION -> DIVISIONIGUAL
OPERACION -> IGUALBOOLEANO
OPERACION -> MENORQUE
OPERACION -> MAYORQUE
OPERACION -> MENORIGUALQUE
OPERACION -> MAYORIGUALQUE
OPERACION -> DIFERENTEDE
OPERACION -> Y
OPERACION -> O
 
DATO -> NCADENA
DATO -> NDECIMAL
DATO -> NENTERO
DATO -> NBOOLEANO

TIPODATO -> TIPOENTERO
TIPODATO -> TIPOCADENA
TIPODATO -> TIPODECIMAL
TIPODATO -> TIPOBOOLEANO

UNARIOS -> AUMENTAR
UNARIOS -> DISMINUIR
PROGRAMA ::= FUNC MAIN

FUNC ::= FUNCION t_ID t_CORCHETEABI PARAMETROS t_CORCHETECERR OPDATO t_LLAVEABI INSTRUCCION MASINSTRUCCION t_LLAVECERR

MAIN ::= FUNCION PRINCIPAL TIPOENTERO t_CORCHETEABI t_CORCHETECERR t_LLAVEABI INSTRUCCION MASINSTRUCCION t_LLAVECERR

OPDATO ::= TIPOVACIO
OPDATO ::= TIPODATO 
PARAMETROS ::= VARIABLE t_ID TIPODATO MASPARAMETROS
MASPARAMETROS ::= t_COMA PARAMETROS
MASPARAMETROS ::= ''

CONDICIONAL ::= SI t_CORCHETEABI EXPRESION t_CORCHETECERR t_LLAVEABI INSTRUCCION MASINSTRUCCION t_LLAVECERR POSIBILIDAD
POSIBILIDAD ::= SINO t_CORCHETEABI EXPRESION t_CORCHETECERR t_LLAVEABI INSTRUCCION MASINSTRUCCION t_LLAVECERR POSIBILIDAD
POSIBILIDAD ::= ENTONCES t_CORCHETEABI EXPRESION t_CORCHETECERR t_LLAVEABI INSTRUCCION MASINSTRUCCION t_LLAVECERR POSIBILIDAD
POSIBILIDAD ::= ''

BUCLEWHILE ::= MIENTRAS t_CORCHETEABI EXPRESION t_CORCHETECERR t_LLAVEABI INSTRUCCION MASINSTRUCCION t_LLAVECERR

BUCLEFOR ::= PARA t_CORCHETEABI VARIABLE t_ID TIPOENTERO t_PUNTOYCOMA EXPRESION t_PUNTOYCOMA t_ID UNARIOS t_CORCHETECERR t_LLAVEABI INSTRUCCION MASINSTRUCCION t_LLAVECERR


INSTRUCCION ::= ASIG
INSTRUCCION ::= IMP
INSTRUCCION ::= BUCLEFOR
INSTRUCCION ::= BUCLEWHILE
INSTRUCCION ::= CONDICIONAL
INSTRUCCION ::= DETENER
INSTRUCCION ::= FUNCN
INSTRUCCION ::= RETORNAR EXPRESION

MASINSTRUCCION ::= INSTRUCCION MASINSTRUCCION
MASINSTRUCCION ::= ''


IMP ::= IMPRIMIR t_CORCHETEABI COMANDOS t_CORCHETECERR
COMANDOS ::= EXPRESION MASCOMANDOS
MASCOMANDOS ::= t_CONCATENAR COMANDOS
MASCOMANDOS ::= ''

ASIG ::= VARIABLE t_ID TIPODATO OPC
ASIG ::= t_ID t_IGUAL EXPRESION
OPC ::= t_IGUAL EXPRESION
OPC ::= ''

EXPRESION ::= t_CORCHETEABI EXPRESION t_CORCHETECERR
EXPRESION ::= t_ID OPCION MASEXPRESION
EXPRESION ::= DATO MASEXPRESION
MASEXPRESION ::= '' 
MASEXPRESION ::= OPERACION EXPRESION

OPCION ::= t_CORCHETEABI PAR t_CORCHETECERR
OPCION ::= ''

PAR ::= TF RESTO_PARAMETROS
RESTO_PARAMETROS ::= t_COMA TF RESTO_PARAMETROS
RESTO_PARAMETROS ::= ''
TF ::= DATO MASEXPRESION
TF ::= t_ID MASEXPRESION

OPERACION ::= t_SUMA
OPERACION ::= t_RESTA
OPERACION ::= t_MULTIPLICACION
OPERACION ::= t_DIVISION
OPERACION ::= t_RESIDUO
OPERACION ::= t_SUMAIGUAL
OPERACION ::= t_RESTAIGUAL
OPERACION ::= t_MULTIPLICACIONIGUAL
OPERACION ::= t_DIVISIONIGUAL
OPERACION ::= t_IGUAL
OPERACION ::= t_IGUALBOOLEANO
OPERACION ::= t_MENORQUE
OPERACION ::= t_MAYORQUE
OPERACION ::= t_MENORIGUALQUE
OPERACION ::= t_MAYORIGUALQUE
OPERACION ::= t_DIFERENTEDE
OPERACION ::= Y
OPERACION ::= O
OPERACION ::= NO
 
DATO ::= t_NCADENA
DATO ::= t_NDECIMAL
DATO ::= t_NENTERO
DATO ::= NBOOLEANO

TIPODATO ::= TIPOENTERO
TIPODATO ::= TIPOCADENA
TIPODATO ::= TIPODECIMAL
TIPODATO ::= TIPOBOOLEANO

UNARIOS ::= t_AUMENTAR
UNARIOS ::= t_DISMINUIR